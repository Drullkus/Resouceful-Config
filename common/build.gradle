architectury {
    common()
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }

            artifactId = "$archives_base_name-common-$minecraft_version"

            pom {
                name = 'Resourceful Config'
                description = 'Crossplatform config library for Team Resourceful mods and more.'
                url = 'https://github.com/Team-Resourceful/Resouceful-Config'

                scm {
                    connection = 'git:https://github.com/Team-Resourceful/Resouceful-Config.git'
                    developerConnection = 'git:https://github.com/Team-Resourceful/Resouceful-Config.git'
                    url = 'https://github.com/Team-Resourceful/Resouceful-Config'
                }

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://maven.resourcefulbees.com/repository/maven-releases/"
            credentials {
                username ENV.MAVEN_USER
                password ENV.MAVEN_PASS
            }
        }
    }
}